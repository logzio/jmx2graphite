description 'JMX 2 Graphite. Dumps JMX beans into Graphite'

apply plugin:'java'
apply plugin:'application'

mainClassName = 'io.logz.jmx2graphite.Jmx2Graphite'

group = "io.logz"
version = "1.0.3"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.apache.httpcomponents:fluent-hc:4.5.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'io.dropwizard.metrics:metrics-graphite:3.1.2'
    compile 'org.quartz-scheduler:quartz:2.2.2'
    compile 'com.google.inject:guice:4.0'
    compile 'com.typesafe:config:1.3.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.nurkiewicz.asyncretry:asyncretry:0.0.7'
    testCompile 'io.netty:netty-all:4.0.33.Final'
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

distributions {
    main {
        contents {
            into("/opt/jmx2graphite")
        }
    }
}

/*
   Hack to get the conf directory into the classpath
 */
startScripts {
    classpath += files('src/dist/XxxAPlaceHolderForAConfigxxX')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\XxxAPlaceHolderForAConfigxxX', '%APP_HOME%\\conf')
        unixScriptFile.text  = unixScriptFile.text.replace('$APP_HOME/lib/XxxAPlaceHolderForAConfigxxX', '$APP_HOME/conf')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}


