description 'JMX 2 Graphite. Dumps JMX beans into Graphite'

apply plugin:'java'
apply plugin: 'kotlin'
apply plugin:'application'
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = 'io.logz.jmx2graphite.Jmx2GraphiteJolokia'

group = "io.logz"
version = "1.2.2"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.apache.httpcomponents:fluent-hc:4.5.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'io.dropwizard.metrics:metrics-graphite:3.1.2'
    compile 'com.typesafe:config:1.3.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.nurkiewicz.asyncretry:asyncretry:0.0.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.7.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.4'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    testCompile 'io.netty:netty-all:4.0.33.Final'
    testCompile 'junit:junit:4.12'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.8.1'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

distributions {
    main {
        contents {
            into("/opt/jmx2graphite")
        }
    }
}

jar {
    manifest {
        attributes ("Main-Class" : "io.logz.jmx2graphite.Jmx2GraphiteJolokia",
                    "Premain-Class": "io.logz.jmx2graphite.Jmx2GraphiteJavaAgent")
    }
}

task javaAgent(dependsOn: [shadowJar]) {
}

shadowJar {
    manifest.from jar.manifest
    classifier = 'javaagent'
    relocate 'org.apache', 'io.logz.org.apache'
    relocate 'com.fasterxml.jackson', 'io.logz.com.fasterxml.jackson'
    relocate 'io.dropwizard.metrics', 'io.logz.ch.io.dropwizard.metrics'
    relocate 'com.typesafe', 'io.logz.com.typesafe'
    relocate 'com.google', 'io.logz.com.google'
    relocate 'com.nurkiewicz.asyncretry', 'io.logz.com.nurkiewicz.asyncretry'
    relocate 'org.jetbrains.kotlin', 'io.logz.org.jetbrains.kotlin'
}

/*
   Hack to get the conf directory into the classpath
 */
startScripts {
    classpath += files('src/dist/XxxAPlaceHolderForAConfigxxX')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\XxxAPlaceHolderForAConfigxxX', '%APP_HOME%\\conf')
        unixScriptFile.text  = unixScriptFile.text.replace('$APP_HOME/lib/XxxAPlaceHolderForAConfigxxX', '$APP_HOME/conf')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
buildscript {
    ext.kotlin_version = '1.0.1-2'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}


